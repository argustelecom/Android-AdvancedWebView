apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
	//https://github.com/delight-im/Android-AdvancedWebView/issues/83
	compileSdkVersion 23
	buildToolsVersion '26.0.2'

	defaultConfig {
		minSdkVersion 8
		targetSdkVersion 23
	}

	lintOptions {
		//  Error: Google Play requires that apps target API level 26 or higher.
		abortOnError false
	}


	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
		}
	}
}

task sourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier = 'sources'
}

task javadoc(type: Javadoc) {
	failOnError false
	source = android.sourceSets.main.java.sourceFiles
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}
